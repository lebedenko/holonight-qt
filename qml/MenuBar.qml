// SPDX-License-Identifier: GPL-3.0-or-later
// SPDX-FileCopyrightText: 2026 Andrii L <lebeden@gmail.com>

import QtQuick
import QtQuick.Templates as T
import Holonight
import Holonight.Core
import Holonight.impl as Impl

T.MenuBar {
    id: root

    Impl.ControlPalette {
        id: colors
        palette: root.palette
        colorGroup: !root.enabled ? Impl.ControlPalette.Disabled
                                 : (root.Window.window && !root.Window.window.active
                                    ? Impl.ControlPalette.Inactive : Impl.ControlPalette.Active)
        textRole: Impl.ControlPalette.WindowText
        fillRole: Impl.ControlPalette.Button
    }

    implicitWidth: Math.max(implicitBackgroundWidth + leftInset + rightInset,
                            implicitContentWidth + leftPadding + rightPadding)
    implicitHeight: Math.max(implicitBackgroundHeight + topInset + bottomInset,
                             implicitContentHeight + topPadding + bottomPadding)

    topPadding: SafeArea.margins.top
    leftPadding: SafeArea.margins.left
    rightPadding: SafeArea.margins.right
    bottomPadding: SafeArea.margins.bottom

    delegate: MenuBarItem {}

    contentItem: Row {
        spacing: root.spacing
        layoutDirection: root.mirrored ? Qt.RightToLeft : Qt.LeftToRight
        Repeater { model: root.contentModel }
    }

    background: Rectangle {
        implicitHeight: HnMetrics.controlHeight(HnControlSize.Normal)
        color: colors.colors.surfaceRaised
    }
}
